plugins {
    id 'java-library'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.14-beta'
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    mavenCentral()
    // NeoForge 官方
    maven { url = "https://maven.neoforged.net/releases" }

    // Cloth Config
    maven { url = "https://maven.shedaniel.me" }

    // GeckoLib
    maven { url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven" }
}

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = 'gameTestServer'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').absolutePath, '--existing', file('src/main/resources/').absolutePath
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

def mixinRefmapName = "${mod_id}.refmap.json"
def generatedMixinResourcesDir = layout.buildDirectory.dir("generated/mixins")
def generatedRefmapFile = generatedMixinResourcesDir.map { it.file(mixinRefmapName) }
def mixinDefaultObfuscation = findProperty('mixin_default_obfuscation') ?: 'notch'

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    srcDir(generatedMixinResourcesDir)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('compileJava', JavaCompile).configure {
    def refmapFile = generatedRefmapFile.get().asFile
    options.compilerArgs += [
            "-AdefaultObfuscationEnv=${mixinDefaultObfuscation}",
            "-AoutRefMapFile=${refmapFile.absolutePath}"
    ]
    doFirst {
        refmapFile.parentFile.mkdirs()
    }
}

tasks.named('compileTestJava', JavaCompile).configure {
    classpath = classpath.plus(tasks.named('compileJava', JavaCompile).get().classpath)
    options.annotationProcessorPath = tasks.named('compileJava', JavaCompile).get().options.annotationProcessorPath
}

dependencies {
    // ===== Cloth Config (强依赖，运行必需) =====
    compileOnly "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    runtimeOnly "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    testImplementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"

    // ===== GeckoLib (动画库) =====
    // 主模块按需依赖：编译期与运行期均可用（不打包）
    compileOnly "software.bernie.geckolib:geckolib-neoforge-${geckolib_mc}:${geckolib_version}"
    runtimeOnly  "software.bernie.geckolib:geckolib-neoforge-${geckolib_mc}:${geckolib_version}"
    // 测试环境也提供（供单元测试/运行时类可见）
    testImplementation "software.bernie.geckolib:geckolib-neoforge-${geckolib_mc}:${geckolib_version}"

    // 本地 jar（AttributeFix、PlayerAnimator）
    testImplementation files("libs/attributefix-neoforge-1.21.1-21.1.2.jar")
    testImplementation files("libs/player-animation-lib-forge-2.0.1+1.21.1.jar")
    testImplementation files("libs/gzr.jar")
    testImplementation files("libs/Pehkui-3.8.3+1.21-neoforge.jar")

    testImplementation 'com.google.guava:guava:33.3.1-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'


    annotationProcessor 'org.spongepowered:mixin:0.8.7:processor'
}

test {
    useJUnitPlatform()
    classpath += sourceSets.main.runtimeClasspath
}

processResources {
    def replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

tasks.named('processResources').configure {
    dependsOn(tasks.named('compileJava'))
}

jar {
    manifest {
        attributes(
                'Specification-Title'   : mod_id,
                'Specification-Vendor'  : mod_group_id,
                'Specification-Version' : '1',
                'Implementation-Title'  : project.name,
                'Implementation-Version': mod_version,
                'Implementation-Vendor' : mod_group_id,
                'MixinConfigs'          : "${mod_id}.mixins.json"
        )
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
